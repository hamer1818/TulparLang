import "lib/socket.tpr";

print("==================================");
print("      TULPAR CHAT SERVER");
print("==================================");

json server = create_server("127.0.0.1", 8080);

if (server["connected"]) {
    print("Server started on 127.0.0.1:8080");
    print("Waiting for client to connect...");
    
    json client = socket_obj_accept(server);
    
    if (client["connected"]) {
        print("Client connected!");
        print("Chat started. Type 'exit' to quit.");
        print("----------------------------------");
        
        while (true) {
            // Server waits for message first (Turn-based)
            print("Waiting for client message...");
            str msg = socket_obj_receive(client, 1024);
            
            if (msg == "") {
                print("Client disconnected.");
                break;
            }
            
            print("Client: " + msg);
            
            if (msg == "exit") {
                print("Client ended the chat.");
                break;
            }
            
            // Server sends reply
            str reply = input("You: ");
            socket_obj_send(client, reply);
            
            if (reply == "exit") {
                break;
            }
        }
        
        socket_obj_close(client);
    } else {
        print("Failed to accept client connection.");
    }
    
    socket_obj_close(server);
    print("Server stopped.");
} else {
    print("Failed to start server. Port 8080 might be in use.");
}
